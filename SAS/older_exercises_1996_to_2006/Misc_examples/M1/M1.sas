/********************************************************************\

PROGRAM:     	C:\MEPS\PROG\M1.SAS

DESCRIPTION: 	THIS EXAMPLE SHOWS THE NEED FOR USING WEIGHT
		         	VARIABLES WHEN ANALYZING MEPS DATA FOR
		         	NATIONAL ESTIMATES.
		         

INPUT FILE:  	C:\MEPS\DATA\H97.SAS7BDAT (2005 FULL-YEAR DATA FILE)               

\********************************************************************/

FOOTNOTE 'PROGRAM: C:\MEPS\PROG\M1.SAS';

LIBNAME CDATA 'C:\MEPS\DATA';

TITLE1 'AHRQ MEPS DATA USERS WORKSHOP -- SEPTEMBER 2008';                                
TITLE2 'EFFECT OF WEIGHT VARIABLE ON RACE/ETHNICITY PERCENTAGE';

PROC FORMAT;        
	VALUE RACETHNB
	0='TOTAL'
	1 = 'HISPANIC'
	2 = 'BLACK-NO OTH RACE/NOT HISPANIC'
	3 = 'ASIAN-NO OTH RACE/NOT HISPANIC'
	4 = 'OTHER/NOT HISPANIC';
RUN;
 
/***** MODIFY RACE/ETHNICITY CATEGORIES *****/

* READ 2005 CONSOLIDATED FULL YEAR FILE;
DATA PUF97 ;                                                                     
	SET CDATA.H97 (KEEP= RACETHNX PERWT05F);                                      
RUN;

* CREATE UNWEIGHTED INTERNAL FILE;
PROC FREQ DATA=PUF97 NOPRINT;                                                     
	TABLE RACETHNX/ OUT=UNWT(KEEP=RACETHNX PERCENT);
	FORMAT RACETHNX RACETHNB.;
RUN;

* CREATE WEIGHTED INTERNAL FILE;
PROC FREQ DATA=PUF97 NOPRINT;                                                    
	TABLE RACETHNX/ OUT=WT(KEEP=RACETHNX PERCENT);
	FORMAT RACETHNX RACETHNB.;
	WEIGHT PERWT05F;                                                              
RUN;

* MERGE UNWEIGHTED AND WEIGHTED FILES;
DATA PCT;
	MERGE UNWT WT(RENAME=(PERCENT=WTPCT));
	BY RACETHNX;
	FORMAT PERCENT WTPCT 5.2;
	LABEL PERCENT='UNWEIGHTED PERCENTAGE'
	WTPCT='WEIGHTED PERCENTAGE';
RUN;

PROC PRINT DATA=PCT NOOBS LABEL;
	VAR RACETHNX PERCENT WTPCT;
RUN;

