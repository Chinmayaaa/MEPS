**********************************************************************************

PROGRAM:      C:\MEPS\PROG\E6.SAS

DESCRIPTION:  THIS PROGRAM GENERATES THE FOLLOWING ESTIMATES FOR 2005 
              PERCENTAGE DISTRIBUTION OF HEALTH CARE SPENDING, BY TYPE OF SERVICE,
              FOR THE U.S. CIVILIAN NONINSTITUTIONALIZED POPULATION
              AS REFERENCED IN FIGURE 1 OF THE MEPS STAT BRIEF #193
              THE PROGRAM USES 2005 MEPS DATA.

              TYPE OF SERVICE CATEGORIES:
                HOSPITAL INPATIENT
                OFFICE-BASED VISITS
                PRESCRIBED MEDICINES
                HOSPITAL OUTPATIENT
                DENTAL VISITS
                EMERGENCY ROOM
                HOME HEALTH CARE (AGENCY & NON-AGENCY)
                OTHER (TOTAL EXPENDITURES - ABOVE EXPENDITURE CATEGORIES)

              NOTE: EXPENSES INCLUDE BOTH FACILITY AND PHYSICIAN EXPENSES.
  		
INPUT FILE:   C:\MEPS\DATA\H97.SAS7BDAT (2005 FULL-YEAR FILE)
                       
*********************************************************************************
;
              
FOOTNOTE 'PROGRAM: C:\MEPS\PROG\E6.SAS';

LIBNAME CDATA 'C:\MEPS\DATA';

TITLE1 'AHRQ MEPS DATA USERS WORKSHOP -- SEPTEMBER 2008';


/* Read in data from 2005 Consolidated Data File (HC-097) */

DATA PUF97 (KEEP= TOTAL HOSPITAL_INPATIENT OFFICE_BASED PRESCRIBED_MEDICINES
                  HOSPITAL_OUTPATIENT DENTAL EMERGENCY_ROOM HOME_HEALTH
                  OTHER 
                  X_ANYSVCE X_HOSPITAL_INPATIENT X_OFFICE_BASED X_PRESCRIBED_MEDICINES
                  X_HOSPITAL_OUTPATIENT X_DENTAL X_EMERGENCY_ROOM X_HOME_HEALTH X_OTHER
                  VARSTR VARPSU PERWT05F);

  SET CDATA.H97 (KEEP= TOTEXP05 IPDEXP05 IPFEXP05 OBVEXP05 RXEXP05 
	                       OPDEXP05 OPFEXP05 DVTEXP05 ERDEXP05 ERFEXP05
	                       HHAEXP05 HHNEXP05 OTHEXP05 VARSTR VARPSU PERWT05F);
	                        
  IF PERWT05F>0;


  /* Define expenditure variables by type of service  */
  
  TOTAL = TOTEXP05;
  HOSPITAL_INPATIENT   = IPDEXP05 + IPFEXP05;
  OFFICE_BASED         = OBVEXP05;
  PRESCRIBED_MEDICINES = RXEXP05;
  HOSPITAL_OUTPATIENT  = OPDEXP05 + OPFEXP05; 
  DENTAL               = DVTEXP05;
  EMERGENCY_ROOM       = ERDEXP05 + ERFEXP05;
  HOME_HEALTH          = HHAEXP05 + HHNEXP05;
  OTHER                = TOTAL - (HOSPITAL_INPATIENT + OFFICE_BASED + PRESCRIBED_MEDICINES +
                                  HOSPITAL_OUTPATIENT + DENTAL + EMERGENCY_ROOM + HOME_HEALTH);                                


  /* Create Flag (1/0) variables for persons with an expense, by type of service  */
   
  ARRAY EXX  (9) TOTAL HOSPITAL_INPATIENT OFFICE_BASED PRESCRIBED_MEDICINES
                 HOSPITAL_OUTPATIENT DENTAL EMERGENCY_ROOM HOME_HEALTH OTHER;
                 
  ARRAY ANYX (9) X_ANYSVCE X_HOSPITAL_INPATIENT X_OFFICE_BASED X_PRESCRIBED_MEDICINES
                 X_HOSPITAL_OUTPATIENT X_DENTAL X_EMERGENCY_ROOM X_HOME_HEALTH X_OTHER;
  
  DO I=1 TO 9;
    IF EXX(I)>0 THEN ANYX(I)=1; 
    ELSE ANYX(I)=0;      
  END;

RUN;

TITLE2 'HEALTHCARE SPENDING, 2005 (MEPS STAT BRIEF #193, 2005 DATA)';
TITLE3 'FIGURE 1: DISTRIBUTION BY TYPE OF SERVICE';
PROC SURVEYMEANS DATA= PUF97 MEAN STDERR SUM STD;
	STRATUM VARSTR;
	CLUSTER VARPSU;
	WEIGHT PERWT05F;
	VAR   HOSPITAL_INPATIENT OFFICE_BASED PRESCRIBED_MEDICINES HOSPITAL_OUTPATIENT 
	      DENTAL EMERGENCY_ROOM HOME_HEALTH OTHER TOTAL;
	RATIO HOSPITAL_INPATIENT OFFICE_BASED PRESCRIBED_MEDICINES HOSPITAL_OUTPATIENT 
	      DENTAL EMERGENCY_ROOM HOME_HEALTH OTHER / TOTAL;
RUN;


TITLE3 'FIGURE 2: PERCENTAGE OF PERSONS WITH AN EXPENSE, BY TYPE OF SERVICE';
PROC SURVEYMEANS DATA= PUF97 MEAN STDERR SUM STD;
	STRATUM VARSTR;
	CLUSTER VARPSU;
	WEIGHT PERWT05F;
	VAR X_ANYSVCE X_HOSPITAL_INPATIENT X_OFFICE_BASED X_PRESCRIBED_MEDICINES
	    X_HOSPITAL_OUTPATIENT X_DENTAL X_EMERGENCY_ROOM X_HOME_HEALTH X_OTHER;
RUN;

