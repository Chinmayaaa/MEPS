/****************************************************************\

PROGRAM:       C:\MEPS\PROG\EXAMPLE_E2.SAS

DESCRIPTION:  	THIS EXAMPLE SHOWS HOW TO 
               (1) COMPUTE AVERAGE TOTAL HEALTHCARE EXPENDITURES
                   FOR CHILDREN 0-5 FOR THE 4 YEARS 1996-1999.
               (2) COMPUTE AVERAGE TOTAL EXPENDITURES FOR 
                   1996-1997 AND 1998-1999 USING POOLED DATA.  

EXPENDITURES:  FOR THE POOLED DATA FILES, EXPENDITURES ARE 
               STANDARDIZED TO 1999 DOLLARS USING THE ANNUAL CPI 
               FOR ALL URBAN CONSUMERS (CPI-U) FOR THE U.S. CITY
               AVERAGE FOR ALL ITEMS, 1982-84=100.  A TABLE WITH 
               THIS DATA IS AVAILABLE AT THE CPI HOME PAGE:
               http://www.bls.gov/cpi/home.htm.
               
               ANNUAL CPI-U (1996) = 156.9
               ANNUAL CPI-U (1997) = 160.5
               ANNUAL CPI-U (1998) = 163.0
               ANNUAL CPI-U (1999) = 166.6

INPUT FILES:  	(1) C:\MEPS\DATA\H12.SD7 (1996 FULL-YEAR DATA FILE)
               (2) C:\MEPS\DATA\H20.SD7 (1997 FULL-YEAR DATA FILE)
               (3) C:\MEPS\DATA\H28.SD7 (1998 FULL-YEAR DATA FILE)
               (4) C:\MEPS\DATA\H38.SD7 (1999 FULL-YEAR DATA FILE)
               (5) C:\MEPS\DATA\H36.SD7 
                                (1996-2002 POOLED ESTIMATION FILE)

\****************************************************************/

LIBNAME CMEPS V8 'C:\MEPS\DATA';

FOOTNOTE 'PROGRAM: C:\MEPS\PROG\PMED\EXAMPLE_E2.SAS';

TITLE1 'AHRQ MEPS DATA USERS WORKSHOP (ESTIMATION) -- NOV/DEC 2004';
TITLE2 'AVERAGE TOTAL HEALTHCARE EXPENDITURES, 1996 - 1999';
TITLE3 ' ';

PROC FORMAT;
   VALUE AGEF
   0-5 = 'AGE 0-5'
   OTHER = 'OTHER';
RUN;

/***** THESE DATA STEPS READ IN THE REQUIRED VARIABLES FROM THE *****/
/***** FULL-YEAR FILES.                                         *****/

/***** AGEyyX VARIABLE USED TO DISTINGUISH THE 0-5 POPULATION.  *****/

/*****                     1996 DATA                            *****/
/***** FOR LATER POOLING, CREATE A VARIABLE 'TOTEXP' THAT IS    *****/
/***** STANDARDIZED TO 1999 DOLLARS BY MULTIPLYING TOTEXP96 BY  *****/
/***** (CPI_1999 / CPI_1996).                                   *****/ 

DATA H12;
   SET CMEPS.H12 (KEEP= DUPERSID TOTEXP96 WTDPER96 VARPSU96 
                           VARSTR96 AGE96X);
   TOTEXP = TOTEXP96 * (166.6/156.9);
RUN;

/*****                    1997 DATA                            *****/
/***** FOR LATER POOLING, CREATE A VARIABLE 'TOTEXP' THAT IS    *****/
/***** STANDARDIZED TO 1999 DOLLARS BY MULTIPLYING TOTEXP97 BY  *****/
/***** (CPI_1999 / CPI_1997).                                   *****/ 

DATA H20;
   SET CMEPS.H20 (KEEP= DUPERSID TOTEXP97 WTDPER97 VARPSU97 
                           VARSTR97 AGE97X);
   TOTEXP = TOTEXP97 * (166.6/160.5);
RUN;

/*****                    1998 DATA                             *****/
/***** FOR LATER POOLING, CREATE A VARIABLE 'TOTEXP' THAT IS    *****/
/***** STANDARDIZED TO 1999 DOLLARS BY MULTIPLYING TOTEXP98 BY  *****/
/***** (CPI_1999 / CPI_1998).                                   *****/ 

DATA H28;
   SET CMEPS.H28 (KEEP= DUPERSID TOTEXP98 WTDPER98 VARPSU98 
                           VARSTR98 AGE98X);
   TOTEXP = TOTEXP98 * (166.6/163.0);
RUN;

/*****                    1999 DATA                             *****/

DATA H38;
   SET CMEPS.H38 (KEEP= DUPERSID TOTEXP99 PERWT99F VARPSU99 
                           VARSTR99 AGE99X);
RUN;

TITLE4 '1996 DATA';

PROC SURVEYMEANS DATA= H12 NOBS SUMWGT MEAN STDERR CLM;
   VAR TOTEXP96;
   STRATA VARSTR96;
   CLUSTER VARPSU96;
   WEIGHT WTDPER96;
   DOMAIN AGE96X;
   FORMAT AGE96X AGEF. ;
RUN;

TITLE4 '1997 DATA';

PROC SURVEYMEANS DATA= H20 NOBS SUMWGT MEAN STDERR CLM;
   VAR TOTEXP97;
   STRATA VARSTR97;
   CLUSTER VARPSU97;
   WEIGHT WTDPER97;
   DOMAIN AGE97X;
   FORMAT AGE97X AGEF. ;
RUN;

TITLE4 '1998 DATA';

PROC SURVEYMEANS DATA= H28 NOBS SUMWGT MEAN STDERR CLM;
   VAR TOTEXP98;
   STRATA VARSTR98;
   CLUSTER VARPSU98;
   WEIGHT WTDPER98;
   DOMAIN AGE98X;
   FORMAT AGE98X AGEF. ;
RUN;

TITLE4 '1999 DATA';

PROC SURVEYMEANS DATA= H38 NOBS SUMWGT MEAN STDERR CLM;
   VAR TOTEXP99;
   STRATA VARSTR99;
   CLUSTER VARPSU99;
   WEIGHT PERWT99F;
   DOMAIN AGE99X;
   FORMAT AGE99X AGEF. ;
RUN;

/***** POOL 1996 AND 1997 DATA                                  *****/
/***** DIVIDE WEIGHT VARIABLE (POOLWT) by 2.                    *****/

DATA POOL9697 (KEEP= DUPERSID TOTEXP AGE POOLWT);
   SET   H12 (RENAME=(WTDPER96 = POOLWT AGE96X = AGE))
         H20 (RENAME=(WTDPER97 = POOLWT AGE97X = AGE)); 
   IF POOLWT > 0
      THEN POOLWT = POOLWT / 2 ;
RUN;

/***** GET STRATUM AND PSU VARIABLES FROM HC-036.               *****/

DATA H36;
   SET CMEPS.H36 (KEEP= DUPERSID STRA9602 PSU9602);
RUN;

PROC SORT DATA= POOL9697;
   BY DUPERSID;
RUN;

DATA POOL9697;
   MERGE POOL9697 (IN= A) H36 (IN= B);
   BY DUPERSID;
   IF A AND B;
RUN;

TITLE4 'POOLED 1996-1997 DATA';
TITLE5 'EXPENDITURES STANDARDIZED TO 1999 DOLLARS';

PROC SURVEYMEANS DATA= POOL9697 NOBS SUMWGT MEAN STDERR CLM;
   VAR TOTEXP;
   STRATA STRA9602;
   CLUSTER PSU9602;
   WEIGHT POOLWT;
   DOMAIN AGE;
   FORMAT AGE AGEF. ;
RUN;

/***** POOL 1998 AND 1999 DATA                                  *****/
/***** DIVIDE WEIGHT VARIABLE (POOLWT) by 2.                    *****/

DATA POOL9899 (KEEP= DUPERSID TOTEXP AGE POOLWT);
   SET   H28 (RENAME=(WTDPER98 = POOLWT AGE98X = AGE))
         H38 (RENAME=(TOTEXP99= TOTEXP PERWT99F = POOLWT 
                           AGE99X = AGE)); 
   IF POOLWT > 0
      THEN POOLWT = POOLWT / 2 ;
RUN;

/***** MERGE ON *STRA9602* AND *PSU9602* FROM HC036             *****/

PROC SORT DATA= POOL9899;
   BY DUPERSID;
RUN;

DATA POOL9899;
   MERGE POOL9899 (IN= A) H36 (IN= B);
   BY DUPERSID;
   IF A AND B;
RUN;

TITLE4 'POOLED 1998-1999 DATA';
TITLE5 'EXPENDITURES STANDARDIZED TO 1999 DOLLARS';

PROC SURVEYMEANS DATA= POOL9899 NOBS SUMWGT MEAN STDERR CLM;
   VAR TOTEXP;
   STRATA STRA9602;
   CLUSTER PSU9602;
   WEIGHT POOLWT;
   DOMAIN AGE;
   FORMAT AGE AGEF. ;
RUN;


