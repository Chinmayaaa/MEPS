/***************************************************************************\

PROGRAM:	C:\MEPS\PROG\E8.SAS

DESCRIPTION:	THIS PROGRAM GENERATES TWO SETS OF EXPENDITURE DATA FOR 2005
							INPATIENT STAYS, SIMILAR TO THOSE REPORTED IN MEPS STAT BRIEF
							#164 (FIGURES 1 AND 5). STAT BRIEF #164 USED 2004 MEPS DATA.  
							THIS PROGRAM USES 2005 MEPS DATA.

							ESTIMATES COMPARABLE TO FIGURE 1 ARE BY EVENT TYPE AND BY
							SOURCE OF PAYMENT FOR INPATIENT (IP) STAY EXPENSES.

							EVENT TYPE DEFINITIONS:

							AMBULATORY:  OFFICE-BASED + OUTPATIENT + EMERGENCY DEPARTMENT
							HOSPITAL:    HOSPITAL INPATIENT
							PRESCRIBED   MEDICINES: PMED (RX)
							OTHER:       TOTAL EXPENDITURES - (AMBULATORY + HOSPITAL + PMED)

							NOTES: (1) EXPENSES INCLUDE BOTH FACILITY AND PHYSICIAN EXPENSES.
       							 (2) PRIVATE INCLUDES TRICARE, BUT NOT 'OTHER PRIVATE'.

		    TOTAL HEALTH CARE EXP: $963,882,049,581 
                       (SB: $963.9 BILLION)

     		EVENT TYPES:  2005 DATA    2004 DATA (SB#164)
     
        PMED:   			20.8%		     19.8%
        HOSPITAL:     30.9%        31.4%
        AMBULATORY:   34.7%        35.6%
        OTHER:        13.5%        13.2%
        
        FOR HOSPITAL EXPENSES (SB #164)
        FIRST BULLET IN HIGHLIGHTS STATES, 
        "EXPENSES FOR HOSPITAL INPATIENT STAYS
        ACCOUNTED FOR NEARLY ONE-THIRD OF ALL
        HEALTH CARE EXPENSES..." [30.9% IN 2005]        
        
        IP SOPs:       2005 DATA    2004 DATA (SB#164)
             
     		PRIVATE:       41.1%        43.7%
     		MEDICARE:      37.7%        38.8%
     		MEDICAID:      10.6%         9.6%
     		OUT-OF-POCKET:  3.8%         2.2%
     		OTHER:          6.9%         5.7%
     		
     		FIGURE 5: IP EXPENSES PER STAY AND PER DIEM
     		
     		PER STAY      2005 DATA     2004 DATA (SB#164)
     		
     		W/  SURG       $15,160       $14,729
     		W/0 SURG       $ 8,439       $ 7,678
     		
     		PER DIEM
     		
     		W/  SURG       $ 5,226       $ 5,059
     		W/O SURG       $ 2,173       $ 1,954
     		
     		NOTE: FOR THE SMALL NUMBER OF ZERO NIGHT STAYS, LENGTH
     		      OF STAY WAS CONSIDERED TO BE ONE.
     		
INPUT FILES: 	(1) C:\MEPS\DATA\H97.SAS7BDAT (2005 FULL-YEAR FILE)
              (2) C:\MEPS\DATA\H94D.SAS7BDAT (2005 HOSP. IP STAYS)     

\******************************************************************/

FOOTNOTE 'PROGRAM: C:\MEPS\PROG\E8.SAS';

LIBNAME CDATA 'C:\MEPS\DATA';

TITLE1 'AHRQ MEPS DATA USERS WORKSHOP -- SEPTEMBER 2008';

PROC FORMAT;
	VALUE SURGF
	1 = 'SURGERY'
	OTHER = 'OTHER';
RUN;

/* READ IN DATA FROM 2005 CONSOLIDATED DATA FILE (HC-097) */

DATA PUF97 (KEEP= TOTAL PRESCRIBED_MEDICINES HOSPITAL_INPATIENT 
                   AMBULATORY_CARE OTHER 
                   IP_TOTAL IP_PRIVATE_INS IP_MEDICARE IP_MEDICAID 
                   IP_OUT_OF_POCKET IP_OTHER VARSTR VARPSU PERWT05F);
	SET CDATA.H97 (KEEP= TOTEXP05 RXEXP05 OBVEXP05 OPDEXP05 OPFEXP05 
												  ERDEXP05 ERFEXP05 IPDEXP05 IPFEXP05 
												  IPDPRV05 IPFPRV05 IPDTRI05 IPFTRI05
												  IPDMCR05 IPFMCR05 IPDMCD05 IPFMCD05 
												  IPDSLF05 IPFSLF05 
												  VARSTR VARPSU PERWT05F);
  
	TOTAL = TOTEXP05;
  PRESCRIBED_MEDICINES = RXEXP05;
  HOSPITAL_INPATIENT = (IPDEXP05 + IPFEXP05);
  AMBULATORY_CARE = (OBVEXP05 + OPDEXP05 + OPFEXP05 + ERDEXP05 + ERFEXP05);
  OTHER = TOTAL - (PRESCRIBED_MEDICINES + HOSPITAL_INPATIENT + AMBULATORY_CARE);
	
	IP_TOTAL = HOSPITAL_INPATIENT;
  IP_PRIVATE_INS = (IPDPRV05 + IPFPRV05) + (IPDTRI05 + IPFTRI05);
  IP_MEDICARE = IPDMCR05 + IPFMCR05;
  IP_MEDICAID = IPDMCD05 + IPFMCD05;
  IP_OUT_OF_POCKET = IPDSLF05 + IPFSLF05;
  IP_OTHER = IP_TOTAL - (IP_PRIVATE_INS+IP_MEDICARE+IP_MEDICAID+IP_OUT_OF_POCKET);  
RUN;

TITLE2 'HEALTHCARE EXPENSES, 2005 (CF. MEPS STAT BRIEF #164, 2004 DATA)';
TITLE3 'FIGURE 1: DISTRIBUTION BY EVENT TYPE';

PROC SURVEYMEANS DATA= PUF97 MEAN STDERR SUM STD;
	STRATUM VARSTR;
	CLUSTER VARPSU;
	WEIGHT PERWT05F;
	VAR   PRESCRIBED_MEDICINES HOSPITAL_INPATIENT AMBULATORY_CARE OTHER TOTAL;
	RATIO PRESCRIBED_MEDICINES HOSPITAL_INPATIENT AMBULATORY_CARE OTHER / TOTAL;
RUN;

TITLE3 'FIGURE 1: DISTRIBUTION OF INPATIENT (IP) EXPENSES BY SOURCE OF PAYMENT';

PROC SURVEYMEANS DATA= PUF97 MEAN STDERR SUM STD;
	STRATUM VARSTR;
	CLUSTER VARPSU;
	WEIGHT PERWT05F;
	VAR   IP_PRIVATE_INS IP_MEDICARE IP_MEDICAID IP_OUT_OF_POCKET IP_OTHER IP_TOTAL;
	RATIO IP_PRIVATE_INS IP_MEDICARE IP_MEDICAID IP_OUT_OF_POCKET IP_OTHER / IP_TOTAL;
RUN;

/* READ IN DATA FROM 2005 HOSPITAL INPATIENT STAYS DATA FILE (HC-094D) */

DATA IP2005;
	SET CDATA.H94D (KEEP= DUPERSID EVNTIDX RSNINHOS IPXP05X NUMNIGHX
	                      PERWT05F VARSTR VARPSU);
	IF NUMNIGHX = 0
		THEN NUMNIGHX = 1;
	PERDIEM = ROUND(IPXP05X/NUMNIGHX);
RUN;

TITLE3 'FIGURE 5: AVERAGE IP EXPENSES PER STAY WITH AND WITHOUT SURGERY';

PROC SURVEYMEANS DATA= IP2005 MEAN STDERR;
	STRATUM VARSTR;
	CLUSTER VARPSU;
	WEIGHT PERWT05F;
	VAR   IPXP05X;
	DOMAIN RSNINHOS ;
	FORMAT RSNINHOS SURGF. ;
RUN;

TITLE3 'FIGURE 5: AVERAGE IP EXPENSES PER DIEM WITH AND WITHOUT SURGERY';

PROC SURVEYMEANS DATA= IP2005 MEAN STDERR;
	STRATUM VARSTR;
	CLUSTER VARPSU;
	WEIGHT PERWT05F;
	VAR   PERDIEM;
	DOMAIN RSNINHOS ;
	FORMAT RSNINHOS SURGF. ;
RUN;