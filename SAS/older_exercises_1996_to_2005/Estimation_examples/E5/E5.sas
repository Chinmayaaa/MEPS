/****************************************************************\

PROGRAM:       C:\MEPS\PROG\EXAMPLE_E5.SAS

DESCRIPTION:  	THIS EXAMPLE SHOWS HOW TO COMPUTE EVENT-LEVEL 
               ESTIMATES.
               IN THIS PROGRAM 
               (1) THE 2001 MEPS INPATIENT HOSPITAL STAYS FILE IS 
                   USED TO COMPUTE MEAN FACILITY EXPENSE PER 
                   STAY.
               (2) THE 2001 MEPS OFFICE-BASED MEDICAL PROVIDER
                   VISITS FILE IS USED TO COMPUTE MEAN 
                   EXPENSE PER OFFICE VISIT TO A MEDICAL
                   PROVIDER. 

INPUT FILE:  	(1) C:\MEPS\DATA\H59D.SAS7BDAT 
                        -- 2001 HOSPITAL STAYS FILE
               (2) C:\MEPS\DATA\H59G.SAS7BDAT
                        -- 2001 OFFICE-BASED VISITS FILE

\****************************************************************/

LIBNAME CMEPS V8 'C:\MEPS\DATA' ;

FOOTNOTE 'PROGRAM: C:\MEPS\PROG\EXAMPLE_E5.SAS';

TITLE1 'AHRQ MEPS DATA USERS WORKSHOP (ESTIMATION) -- NOV/DEC 2004';
TITLE2 'COMPUTING EVENT-LEVEL ESTIMATES';
TITLE3 ' ';

/***** THESE DATA STEPS READS IN THE REQUIRED VARIABLES FROM THE *****/
/***** FULL-YEAR FILES.                                          *****/

/***** INPATIENT HOSPITAL STAYS FILE.                            *****/
/***** *IPFXP01X* IS THE TOTAL EXPENSE VARIABLE.                 *****/

TITLE4 'MEAN FACILITY EXPENSE PER INPATIENT STAY' ;

DATA IP2001;
   SET CMEPS.H59D (KEEP= DUPERSID IPFXP01X PERWT01F VARSTR01 
                                 VARPSU01);
   LABEL IPFXP01X = 'TOTAL HOSP. STAY EXPENSES';
RUN;

PROC SURVEYMEANS DATA= IP2001 NOBS SUMWGT MEAN STDERR CLM;
   VAR IPFXP01X;
   STRATA VARSTR01;
   CLUSTER VARPSU01;
   WEIGHT PERWT01F;
RUN; 

/***** OFFICE BASED MEDICAL PROVIDER VISITS FILE.                 *****/
/***** *OBXP01X* IS THE TOTAL EXPENSE VARIABLE.                   *****/

TITLE4 'MEAN EXPENSE PER OFFICE VISIT TO A MEDICAL PROVIDER' ;

DATA OB2001;
   SET CMEPS.H59G (KEEP= DUPERSID OBXP01X PERWT01F VARSTR01 
                                       VARPSU01);
   LABEL OBXP01X = 'TOTAL OFFICE VISIT EXPENSES';
RUN;

PROC SURVEYMEANS DATA= OB2001 NOBS SUMWGT MEAN STDERR CLM;
   VAR OBXP01X;
   STRATA VARSTR01;
   CLUSTER VARPSU01;
   WEIGHT PERWT01F;
RUN; 

